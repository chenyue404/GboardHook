import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = file("./AndroidKeystore/keystore.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.chenyue404.gboardhook"
        minSdk 23
        targetSdk 31
        versionCode 8
        versionName "1.1.0"
    }
    lintOptions {
        disable 'ExpiredTargetSdkVersion'
    }
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                // 如果文件不存在，可以给出默认提示或抛异常
                println "Warning: keystore.properties not found, using debug signing."
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // 获取版本名和版本号
            def versionName = variant.versionName
            def versionCode = variant.versionCode

            // 获取 buildType 和 flavor
            def buildType = variant.buildType.name
            def flavor = variant.flavorName ?: "" // 如果没有 flavor，则为空
            def environment = flavor ? "${flavor}_${buildType}" : buildType

            // 获取当前 UTC 时间
            def date = new Date()
            def sdf = new SimpleDateFormat("yyyyMMdd_HHmmss")
            sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
            def formattedDate = sdf.format(date) + "Z"

            // 应用名称
            def appName = "GboardHook"

            // 构造 APK 文件名
            def apkName = "${appName}_${environment}_${versionName}_${versionCode}_${formattedDate}.apk"

            // 设置输出文件名
            output.outputFileName = apkName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'com.chenyue404.gboardhook'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
//    debugImplementation 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
    implementation 'org.luckypray:dexkit:2.0.6'
}